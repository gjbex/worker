#!/usr/bin/env perl

use strict;
use warnings;
use Config::General;
use Data::Dumper;
use FindBin;
use Getopt::Long;

sub BEGIN {
    if (exists $ENV{WORKER_DIR} && defined $ENV{WORKER_DIR} &&
            length($ENV{WORKER_DIR}) > 0) {
        unshift(@INC, "$ENV{WORKER_DIR}/lib/perl");
    } else {
        unshift(@INC, "$FindBin::Bin/../lib/perl");
    }
}

use Worker::LogAnalyzer;
use Worker::TaskfileParser;
use Worker::Utils;

# directory containing the Worker software
my $worker_dir = find_worker_dir();

my $config_file = "${worker_dir}/conf/worker.conf";
check_file($config_file, 'configuration file', 1);
msg("reading config file '$config_file'...");
my $config = Config::General->new($config_file);
my %conf = $config->getall();
msg("config file read");
msg(Dumper(\%conf) . "\n");

# determine task separator
my $worker_hdr = "${worker_dir}$conf{worker_hdr}";  # worker header file
my $separator = parse_hdr($worker_hdr);

# get the templates for the log and task file
my $default_log = $conf{default_log};    # templ. log name
my $default_sh  = $conf{default_sh};     # templ. batch name
my $default_sql = $conf{default_sql};    # templ. sql DB name

# command line options
my $is_workitems = 0;
my $is_workers = 0;
my $should_keep = undef;
my $verbose = undef;

GetOptions(
    "workers"      => \$is_workers,
    "workitems"    => \$is_workitems,
    "keep"         => \$should_keep,
    "verbose"      => \$verbose,
    "help"         => \&show_help
);

# set verbosity for Worker::Utils
$Worker::Utils::verbose = $verbose;

if ($is_workers && $is_workitems) {
    print STDERR "### error: use either -workers, or -workitems\n";
    exit 1;
}

my $log_ext = compute_file_extension($default_log);
my $batch_ext = compute_file_extension($default_sh);
my $sql_ext = compute_file_extension($default_sql);

my $log_file = $ARGV[0];
check_file($log_file, "log file");

my $task_file = compute_aux_file($log_file, $log_ext, $batch_ext);
check_file($task_file, "task file");

my $sql_file = compute_aux_file($log_file, $log_ext, $sql_ext);

my $task_parser = Worker::TaskfileParser->new($separator);
$task_parser->parse($task_file);
my $nr_tasks = $task_parser->nr_tasks();

my $log_analyzer = undef;
my $sql_exists = -e $sql_file;
if ($should_keep) {
    $log_analyzer = Worker::LogAnalyzer->new($sql_file);
    $log_analyzer->parse($log_file) unless $sql_exists;
} else {
    $log_analyzer = Worker::LogAnalyzer->new();
    $log_analyzer->parse($log_file);
}

if ($is_workitems) {
    my $results = $log_analyzer->work_item_times();
    printf("%12s,%12s,%15s\n",
           'worker item', 'worker ID', 'execution time');
    foreach my $result (@$results) {
        printf("%12d,%12d,%15d\n",
               $result->[0], $result->[1], $result->[2]);
    }
} elsif ($is_workers) {
    my $results = $log_analyzer->worker_times();
    printf("%12s,%15s,%12s\n",
           'worker ID', 'execution time', 'work items');
    foreach my $result (@$results) {
        printf("%12d,%15d,%12d\n",
               $result->[0], $result->[1], $result->[2]);
    }
} else {
    my $results = undef;

    print "Work item statistics:\n";
    $results = $log_analyzer->work_item_stats();
    printf "\tnumber of items: %d\n", ($nr_tasks);
    printf "\tnumber of finished items: %d\n", ($results->[0]->[0]);
    printf "\taverage execution time: %.2f\n", ($results->[0]->[1]);
    printf "\tminimum execution time: %d\n", ($results->[0]->[2]);
    printf "\tmaximum execution time: %d\n", ($results->[0]->[3]);

    print "Worker statistics:\n";
    $results = $log_analyzer->worker_stats();
    printf "\tnumber of workers: %d\n", ($results->[0]->[0]);
    printf "\taverage execution time: %.2f\n", ($results->[0]->[1]);
    printf "\tminimum execution time: %d\n", ($results->[0]->[2]);
    printf "\tmaximum execution time: %d\n", ($results->[0]->[3]);
}

# ---------------------------------------------------------------------
# compute the name of a task file out of that of the given log file name
# ---------------------------------------------------------------------
sub compute_aux_file {
    my ($log_file, $log_ext, $new_ext) = @_;
    $new_ext = ".$new_ext";
    $log_file =~ s/\.$log_ext([^.]*)$/$new_ext.$1/e;
    return $log_file;
}

# ---------------------------------------------------------------------
# print the script's help stuff
# ---------------------------------------------------------------------
sub print_help {
    print STDERR <<EOI
### usage: wload [-help] [-workitems | -workers] [-keep]  \\
#                [-verbose] <log-file>
#
#   -workitems : report on inidiviudal work items
#   -workers   : report on individual workers
#   -keep      : keep generated SQLite3 database
#   -verbose   : show feedback while processing
#   -help      : print this help message
#   <log-file> : worker log files to parse
#
# Utility to perform a more detailed aanlysis on a worker run, provides
# statistics on work items and workers, or more detailed information on
# either for analyzing load balancing issues
EOI
}

# ---------------------------------------------------------------------
# shows help and exits
# ---------------------------------------------------------------------
sub show_help {
    print_help();
    exit 0;
}
# ----------------------------------------------------------------------------
